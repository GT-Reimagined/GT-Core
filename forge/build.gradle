import org.gradle.api.tasks.bundling.Jar

plugins {
    id "net.neoforged.moddev.legacyforge" version "2.0.55-beta"
}

archivesBaseName = "${rootProject.archive_base_name}-forge"

neoForge {
    version = "${project.minecraft_version}-${project.forge_version}"
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = rootProject.minecraft_version
        mappingsVersion = rootProject.mappings_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}




dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation("org.jetbrains:annotations:26.0.1")
    modImplementation("io.github.gregtechintergalactical:antimatter-forge:${antimatter_version}")
    modImplementation("io.github.gregtechintergalactical:tesseract-forge:${tesseract_version}")
    //modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}"
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")
    modCompileOnly("curse.maven:terrafirmacraft-302973:4723717")
    modImplementation("earth.terrarium:botarium-forge-1.18.2:${project.botarium_version}")
    modImplementation("com.teamresourceful.resourcefullib:resourcefullib-forge-1.18.2:${project.resourcefullib_version}")
    modImplementation("dev.trinsdar:terraform-wood-api-v1:${project.terraform_version_forge}")
    jarJar("dev.trinsdar:terraform-wood-api-v1:${project.terraform_version_forge}"){
        version{
            strictly("[3.1.5,)")
            prefer(project.terraform_version_forge)
        }
    }
    //modRuntimeOnly("mezz.jei:jei-${rootProject.minecraft_version}-forge:${rootProject.jei_version}")
    modImplementation("maven.modrinth:carbon-config:${project.carbon_config_forge_version}")
    modRuntimeOnly("curse.maven:the-twilight-forest-227639:4337390")
    implementation("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modImplementation("mezz.jei:jei-${rootProject.minecraft_version}-forge:${rootProject.jei_version}")
    modImplementation("curse.maven:the-one-probe-245211:3965693")
    modImplementation("org.gt-reimagined:structurelib-forge:${project.structurelib_version}-${project.minecraft_version}")
    modImplementation("curse.maven:jade-324717:5079263")
}


tasks.named('compileJava', JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
    inputs.property "version", project.mod_version

    exclude("gtcore.accesswidener")
    filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version
    }
}

sourcesJar {
    from(project(':common').sourceSets.main.allSource)
}

mixin {
    add sourceSets.main, "gtcore.refmap.json"
    config "gtcore.mixins.json"
    config "gtcore.jei.mixins.json"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "MixinConfigs"            : "gtcore.mixins.json",
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : "Trinsdar",
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : "Trinsdar",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task deobfJar(type: Jar) {
    archiveClassifier.set "deobf"
    from sourceSets.main.output
}

artifacts {
    archives deobfJar
}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            artifactId = "gtcore-" + project.name
            from components.java
            artifact deobfJar
        }
    }
    repositories {
        def isCI = System.getenv("CI")
        if (isCI) {
            maven {
                url System.getenv("local_maven_url")
            }
        } else {
            mavenLocal()
        }
    }
}
