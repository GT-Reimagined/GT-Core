import org.gradle.api.tasks.bundling.Jar

plugins {
    //id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    //id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    //id 'org.spongepowered.mixin' version '0.7.+'
    id "net.neoforged.moddev.legacy" version "2.0.61-beta-pr-118-legacy"
}

archivesBaseName = "${rootProject.archive_base_name}-forge"

neoForge {
    version = "${project.minecraft_version}-${project.forge_version}"
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = rootProject.minecraft_version
        mappingsVersion = rootProject.mappings_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}




dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    modImplementation("io.github.gregtechintergalactical:antimatter-forge:${antimatter_version}")
    modImplementation("io.github.gregtechintergalactical:tesseract-forge:${tesseract_version}"){
        exclude(group: "maven.modrinth")
    }
    //modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}"
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")
    modCompileOnly("curse.maven:terrafirmacraft-302973:4723717")
    modImplementation("earth.terrarium:botarium-forge-1.18.2:${project.botarium_version}")
    modImplementation("com.teamresourceful.resourcefullib:resourcefullib-forge-1.18.2:${project.resourcefullib_version}")
    modImplementation("io.github.reforgedmods.terraform-api:terraform-wood-api-v1:${project.terraform_version_forge}")
    jarJar("io.github.reforgedmods.terraform-api:terraform-wood-api-v1:${project.terraform_version_forge}"){
        version{
            strictly("[3.1.5,)")
            prefer(project.terraform_version_forge)
        }
    }
    //modRuntimeOnly("mezz.jei:jei-${rootProject.minecraft_version}-forge:${rootProject.jei_version}")
    modImplementation("maven.modrinth:carbon-config:${project.carbon_config_forge_version}")
    modRuntimeOnly("curse.maven:the-twilight-forest-227639:4337390")
    implementation("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modImplementation("mezz.jei:jei-${rootProject.minecraft_version}-forge:${rootProject.jei_version}")
    modImplementation("curse.maven:the-one-probe-245211:3965693")
    modImplementation("com.gtnewhorizon.structurelib:structurelib-forge:${project.structurelib_version}-${project.minecraft_version}")
}


tasks.withType(JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
    inputs.property "version", project.mod_version

    exclude("gtcore.accesswidener")
    filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version
    }
}

sourcesJar {
    from(project(':common').sourceSets.main.allSource)
}

mixin {
    add sourceSets.main, "gtcore.refmap.json"
    config "gtcore.mixins.json"
    config "gtcore.jei.mixins.json"
}

/*tasks.named('jarJar', Jar).configure {
    archiveClassifier.set("")
}*/

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "MixinConfigs"            : "gtcore.mixins.json",
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : "Trinsdar",
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : "Trinsdar",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    //finalizedBy 'reobfJar'
    //archiveClassifier.set "thin"
}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            artifactId = "gtcore-" + project.name
            from components.java
            //jarJar.component(it)
        }
    }
    repositories {
        def isCI = System.getenv("GITHUB_ACTION")
        def isRELEASE = System.getenv("GITHUB_RELEASE")
        if (isCI && isRELEASE) {
            maven {
                url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        } else {
            mavenLocal()
        }
    }
}