plugins {
    id "xyz.wagyourtail.unimined" version "1.3.9-SNAPSHOT"
}
archivesBaseName = "${rootProject.archive_base_name}-common"

configurations {
    modCompileOnly
    compileOnly.extendsFrom modCompileOnly
    modRuntimeOnly
    runtimeOnly.extendsFrom modRuntimeOnly
}

unimined.minecraft {
    version minecraft_version
    accessWidener {
        accessWidener(file("src/main/resources/gtcore.accesswidener"))
    }

    defaultRemapJar = false

    mappings {
        intermediary()
        mojmap()
        devFallbackNamespace "official"
        devNamespace "mojmap"
        parchment(rootProject.minecraft_version, rootProject.mappings_version)
    }

    mods {
        remap(configurations.modCompileOnly){
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
        remap(configurations.modRuntimeOnly){
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
        modImplementation {
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
    }
}



dependencies {
    implementation "org.ow2.asm:asm:9.7.1"
    implementation "org.ow2.asm:asm-analysis:9.7.1"
    implementation "org.ow2.asm:asm-commons:9.7.1"
    implementation "org.ow2.asm:asm-tree:9.7.1"
    implementation "org.ow2.asm:asm-util:9.7.1"
    implementation "net.fabricmc:sponge-mixin:0.15.3+mixin.0.8.7"
    modImplementation("io.github.gregtechintergalactical:antimatter-common:${antimatter_version}"){
        exclude(group: "maven.modrinth")
        exclude(group: "com.github.Trins-mods")
        //exclude(group: "io.github.gregtechintergalactical")
        exclude(group: "io.github.trinsdar", module: "CT-Fabric-Fluid-Support")
    }
    /*modImplementation("io.github.gregtechintergalactical:tesseract-common:${tesseract_version}"){
        exclude(group: "maven.modrinth")
        exclude(group: "com.github.Trins-mods")
    }*/
    modImplementation("earth.terrarium:botarium-common-1.18.2:${project.botarium_version}")
    implementation("com.gtnewhorizon.structurelib:structurelib-common:${project.structurelib_version}-${project.minecraft_version}")
    modImplementation "com.teamresourceful.resourcefullib:resourcefullib-common-1.18.2:${project.resourcefullib_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:${rootProject.rei_version}"
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-fabric:${rootProject.jei_version}")
    modImplementation("com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_version_fabric}"){
        transitive = false
    }
    modCompileOnly("curse.maven:the-one-probe-fabric-581847:${project.top_fabric_file}")
    modImplementation "maven.modrinth:carbon-config:${project.carbon_config_fabric_version}"
}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            artifactId = "gtcore-" + project.name
            from components.java
        }
    }
    repositories {
        def isCI = System.getenv("GITHUB_ACTION")
        def isRELEASE = System.getenv("GITHUB_RELEASE")
        if (isCI && isRELEASE) {
            maven {
                url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        } else {
            mavenLocal()
        }
    }
}